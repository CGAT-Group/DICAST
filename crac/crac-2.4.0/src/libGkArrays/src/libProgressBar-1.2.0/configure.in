dnl Process this file with autoconf to produce a configure script.

m4_define([VERSION_MAJOR], [1])
m4_define([VERSION_MINOR], [2])
m4_define([VERSION_MICRO], [0])

AC_INIT(
  [Library for manage progress bars],
  [VERSION_MAJOR.VERSION_MINOR.VERSION_MICRO],
  [alban.mancheron@inria.fr],
  [libProgressBar])
AC_CONFIG_SRCDIR([libProgressBar/progressBar.cpp])
AC_REVISION($Revision: 1.9 $)

dnl The most recent interface number that this library implements.
LIB_CURRENT=1
dnl The implementation number of the current interface.
LIB_REVISION=2
dnl The difference between the newest and oldest interfaces that this library implements.
LIB_AGE=1
dnl LIB_VERSION is not the release number.
dnl Please read libtool documentation about versionning
LIB_VERSION=$LIB_CURRENT:$LIB_REVISION:$LIB_AGE
dnl Ending numbers of library will be $((LIB_CURRENT-LIB_AGE)).$LIB_AGE.$LIB_REVISION

AC_SUBST([VERSION])
AC_SUBST([LIB_VERSION])
AC_SUBST([LIB_VERSION_EXT], ["VERSION_MAJOR"_"VERSION_MINOR"_"VERSION_MICRO"])

dnl Every other copy of the package version number gets its value from here
AM_INIT_AUTOMAKE
AM_PROG_LIBTOOL

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AC_CONFIG_HEADERS([libProgressBar/config.h])
AC_CONFIG_MACRO_DIR([m4])

AC_PROG_CC(gcc)
AC_PROG_CXX(g++)

ISODATE=`date +%Y-%m-%d`
AC_SUBST(ISODATE)

ARCH=`uname -m | sed s/i.86/i386/ | sed s/x86_64/amd64/`
AC_SUBST(ARCH)

AC_PATH_PROG(DEB_BUILDER, dpkg)
AC_PATH_PROGS(RPM_BUILDER, [rpmbuild rpm])

AC_CHECK_PROG([TEXI2DVI], [texi2dvi], [texi2dvi])
AC_CHECK_PROG([PDFTEX], [pdftex], [pdftex])
AC_CHECK_PROG([KPSEWHICH], [kpsewhich], [kpsewhich])

BUILD_DOC=true
AS_IF([test "x$TEXI2DVI" != "xtexi2dvi" || test "x$PDFTEX" != "xpdftex"],
      [BUILD_DOC=false
       AC_MSG_WARN([The 'texi2dvi' or 'pdftex' program could not be found. This isn't critical, but it means that you won't be able to create the documentation and thus to make releases.])])

AS_IF([test "x$KPSEWHICH" != "xkpsewhich" || test "$(kpsewhich  -show-path=fmt)" == "/nonesuch"],
      [BUILD_DOC=false
       AC_MSG_WARN([The 'texi2dvi' or 'pdftex' is missing a correct latex installation. This isn't critical, but it means that you won't be able to create the documentation and thus to make releases.])])
AM_CONDITIONAL([BUILD_DOC], [$BUILD_DOC])

AC_C_CONST
AC_C_INLINE
AC_HEADER_MAJOR
AC_FUNC_ALLOCA
AC_STRUCT_TM
AC_STRUCT_ST_BLOCKS
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Checks for header files.
AC_HEADER_STDC([])
AC_CHECK_HEADERS([\
  unistd.h sys/param.h sys/time.h time.h sys/sysmacros.h \
  string.h strings.h memory.h fcntl.h alloca.h \
])

CXXFLAGS="-O6 -Wall --pedantic --ansi"

hacked_prefix="${HACKED_PREFIX:-$prefix}"
if test "x$hacked_prefix" == "xNONE"; then
  hacked_prefix=$ac_default_prefix
fi
AC_SUBST(hacked_prefix)

AC_SUBST_FILE(postrm_script)
postrm_script=$srcdir/postrm
AC_SUBST_FILE(postinst_script)
postinst_script=$srcdir/postinst

AC_CONFIG_FILES([\
  postinst prerm \
  libProgressBar.lsm libProgressBar.spec control \
  libProgressBar.pc Makefile libProgressBar/Makefile \
  doc/Makefile doc/libProgressBar.3 \
], [ \
  chmod 755 postinst && chmod 755 prerm \
])
AC_OUTPUT
